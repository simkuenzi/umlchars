

plugins {
    id 'war'
    id 'net.researchgate.release' version '2.8.1'
    id "org.gretty" version "2.3.1"
    id "maven-publish"
    id "org.hidetake.ssh" version "2.10.1"
}

group 'com.github.simkuenzi'
version version

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'org.glassfish.jersey.core', name: 'jersey-server', version: '2.7'
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet-core', version: '2.7'
    compile group: 'org.thymeleaf', name: 'thymeleaf', version: '3.0.11.RELEASE'
    compile group: 'com.github.simkuenzi', name: 'restforms', version: '0.9'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

gretty {
    contextConfigFile = 'umlchars.xml'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/simkuenzi/umlchars")
            credentials {
                username = System.getenv("GIT_USER")
                password = System.getenv("GIT_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.web)
        }
    }
}

remotes {
    webServer {
        host = System.properties['deployServer']
        user = System.properties['deployUser']
        identity = file(System.properties['deployIdentity'] ?: 'id_rsa')
    }
}


task createProperties(dependsOn: processResources) {
    doLast {
        File d = new File("$buildDir/resources/main/com/github/simkuenzi/umlchars")
        d.mkdirs()
        new File(d, "version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

task deploy(dependsOn: war) {
    doLast {
        ssh.run {
            session(remotes.webServer) {
                put from: war.archivePath, into: System.properties['deployPath']
            }
        }
    }
}

classes {
    dependsOn createProperties
}

release {
    buildTasks = ['publish', 'deploy']
}